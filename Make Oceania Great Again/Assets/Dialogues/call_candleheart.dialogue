~ call_start
是你吗...
天哪...
=> Main

~ call_in
哦，谢天谢地...你终于接电话了！
=> Main

~ Main
我给你打了好多电话，但是一直打不通...
你就连消息也不回，我真的以为你出事了...
（抽泣）
你还好吗，小布？
没被FPI的人盯上吧？

- 不说话
	Player: [speed=0.2]......
	[speed=0.2]......
	小布？
	[speed=0.2]......
	（抽泣）
	我...我知道了...
	永别了，亲爱的...

- 亲爱的，我遇到了麻烦
	Player: 亲爱的，我遇到了麻烦...
	do SoundPlayer.stop_sound(SoundPlayer.Walking)
	=> Alert

- 我没事，你怎么样
	Player: 我目前没什么事，你呢？你怎么样？
	do SoundPlayer.stop_sound(SoundPlayer.Walking)
	=> Alert

- 我是FPI的探员 [if secret_album_lock]
	Player: 我并不是你的女朋友。
	do SoundPlayer.stop_sound(SoundPlayer.Walking)
	...什么！？
	[b]混蛋！！！你们把她怎么了！？
	
	- 冷静些...
		Player: 请冷静一些，烛心小姐...
	
	- 威胁她
		Player: 我收到上面的命令来逮捕你，你最好...

- 告诉她你的身份 [if not secret_album_lock]
	Player: 萤，听我说...
	Player: 我是杰瑞，杰瑞·史密斯。
	杰瑞？！怎么是你？
	我不想牵连到你，就把跟你有关的东西全都删了。
	但，但是...你怎么拿着小布的手机？
	难，难道她...
	Player: 我收到上面的命令来逮捕你，而且这个任务的优先级很高。
	Player: 你到底干了些什么，能让FPI这么关注你？
	啊，是，是这样，我找到了————————
	System: -对方已离线-
	=> hacked

System: -对方已离线-
do call_deferred("ending1")
=> END

~ Alert
[speed=0.2]......
你的声音...
你不是小布...
该死的。
System: -对方已离线-
do call_deferred("ending1")
=> END



~ hacked
哦，杰瑞，杰瑞。
你做了一个蠢透了的决定。
Player: 法，法兰西娅局长？
现在我知道了——你就是那个异端的帮凶，对吧？

- 我可以解释...
	Player: 局长，我，我可以解释...
- 我绝对不会背叛自己的国家
	Player: 不，法兰西娅局长，请相信我，我是绝对不会背叛自己的国家的！

无论你有什么接口，背叛大洋国就是背叛英社。
[b]背叛英社就是背叛至高无上的拿破仑总统！

- Player: 我，我发誓我和那个异端绝对势不两立！
	Player: 抓到她之后，我一定会亲自审问她，逼她把一切都交代出来！
- Player: 请相信我...我是个真正的爱国者啊！
	Player: 我永远...永远会对英社和拿破仑总统保持绝对的忠诚！

不用再说了，杰瑞。
我原本还以为你是个优秀的爱国者呢。
不过很显然你更偏向那个异端分子，而不是你的国家。
你知道吗？仁爱部至今依然存在，而且它是大洋国[color=crimson]四大部门[/color]里最重要的一个。
不过很可惜，它再也不会像老大哥时代那样，把人抓进去之后简简单单洗个脑就放出来。
你身体里的每个部位都有价值，杰瑞。
当然，在你的价值得到实现之前，他们也不可能让你这样卑鄙的叛国贼死的太轻松。

- Player: 不要...
- Player: 求，求您了...

永别了，杰瑞。
仁爱部会好好照顾你的。
do call_deferred("ending1")
=> END




~ final_call

小布，你...
......

- 不说话
	Player: [speed=0.2]......
	[speed=0.2]......
	我...我能听到你的心跳声。
	谢谢你，小布。
	这就够了。
	[speed=0.2]......
	=> END


- 告诉她你的身份 [if not secret_album_lock]
	Player: 萤，听我说...
	Player: 我是杰瑞，杰瑞·史密斯。
	杰瑞？！怎么是你？
	我不想牵连到你，就把跟你有关的东西全都删了。
	但，但是...你怎么拿着小布的手机？
	她去哪儿了？
	Player: 听着，她不在这儿。我收到上面的命令要查清楚你的具体位置，之前给你发消息的一直是我。
	怎么...怎么会这样？
	难，难道她...
	
	- 她很好
		Player: 放心，我和她联系过，她暂时没事。
		啊，那真是太好了...
	
	- 我不知道她的下落
		Player: 抱歉...我也不知道她现在身在何处。
		等等，她没被FPI抓到吗？
		这...这说明她肯定还在外面某个地方，对吧？
		Player: ...也许吧。
	
	
	Player: 我收到上面的命令来逮捕你，而且这个任务的优先级很高。
	Player: 你到底干了些什么？
	
	Player: 好了，现在我得问你一些问题了。
	
	
	
	- 关于你的第一次辩护
		Player: 还记得你的第一次辩护吗？
		啊，我当然还记得那时候的情景...
		但救了你的人不只是我，杰瑞。
		我在调查的时候问过你的同学们，他们都说你是个乐于助人的好帮手。
		我去找过你的母亲，她给我看了你从小到大每一张照片，你的眼睛永远盯着国旗的方向。
		我知道你每个周末都会去当义工，为贫民窟的孩子们教授知识。
		你总是向周围的人表达善意...是因为[color=crimson]你的父亲[/color]要你这么做，不是吗？
		总之，你的行动影响太多人了。
		你的同学们，你的朋友们，你周围的人，你的母亲...
		他们每个人，每个人都紧紧地握住我的手，告诉我一定要为你洗清冤屈。
		当时我只知道，要是像你这样正直的人遭受牢狱之灾的话，那大洋国恐怕离毁灭也不远了。
		所以我才愿意赌上一切去帮你。
		
		- 但现在同样的事情发生在你身上了
			Player: 但是现在，同样的事情发生在你身上了。
			啊，对啊。
			很搞笑，不是吗？我们的身份完全互换了呢。
		
		- 但你现在已经自身难保了
			Player: 但是现在，你却已经自身难保了。
			啊，对啊。
			很搞笑，不是吗？我这样的人成了逃犯，成了异端分子。
		
		这次，我真的觉得大洋国没有任何希望了。
		
		
		当然，法制不法制已经算是最微不足道的问题了。
		很多英社的黑料都是真的。
		我以前就只是盲目地相信那些都是欧亚国捏造出来的卑鄙的谣言，是因为他们自己干过才这么说的。
		差点撞上双塔的飞机，靠谋杀继承总统的传说，德克病毒的起源...
		老大哥时代惨绝人寰的暴政，大萧条的尸横遍野，双重思想...
		还有所有人都避而不谈的1984...
		[speed=0.2][b]那些全是真的。
		先自己制造天灾人祸，然后再扮演伟大的拯救者的角色救人民于水火...
		他们一直，一直都在玩这一套。
		
		每一次，每一次当我看到有人遭遇不公的时候，我都会向着塔的方向祈祷：
		啊，神圣真主，啊，伟大的拿破仑总统...
		请带那些遭受苦痛之人脱离苦海吧！
		请为那些无辜之人降下正义吧！
		但...但我怎么能怎么傻呢？
		
		所有的这些苦难...都是我信仰的人造成的。
			
	
	
	- 为什么你会被FPI追杀？
		Player: 你到底干了些什么，能让FPI这么关注你？
		啊，我好像还没有告诉你...
		我...
		......
		好吧，事情是这样的...
		去年的时候，我们吵了一架。
		小布告诉我英社和大洋国都是建立在谎言之上的。
		但我那时还是坚持认为任何对大洋国的怀疑就是在背叛她，背叛我们脚下的这片土地。
		之后我们就分开了，也没再联系。
		当时我，我甚至差点想要把她举报给FPI...
		（抽泣）
		我真傻，真的。
		
		Player: 没事，萤。慢慢说吧。
		在那之后不久，我接了一个案子。
		是一桩妇女拐卖案。
		她被关在一间很黑的小房间里。被发现的时候，脖子上甚至还被铁链锁着。
		她的丈...买下她的男人，逼她生了很多孩子。
		她本来可以拥有自己的人生，拥有一个幸福的家庭的...
		[b]这种事情...这种事情怎么能在我爱的这片土地发生啊！？
		
		Player: ......
		我愤怒到了极点，发誓一定要为受害者讨回正义。
		...但在法庭上我输了。输得很滑稽。
		在审判结束的时候我看了一眼法官的电脑。
		哈，哈哈哈哈哈...
		原来，判决早在开庭前就已经被英社高层的那些人决定了。
		他们甚至还因为法官动作太慢而感到不满，要他别再浪费时间了。
		也就是那一刻，我才意识到：
		大洋国一直以来宣传的“法制”完全就是个笑话。
		作为一名女性，我绝对，绝对没法忍受这种事情...
		我的直觉告诉我她绝不是唯一的受害者。
		于是，之后我花了几天跟踪那个买下她的男人，调查他的工作，行为，人际关系...
		...你猜怎么着？
		他跟一个叫“真理会”的神秘组织有关系。
		
		- Player: 真理会？
			没错。他们的标志是一个长着独眼的三角形。
			有传闻说他们是控制整个国家的财阀组织，而为首的就是[color=crimson]大洋国最大的资本家[/color]。
#			才是真正在背后操控这个国家的人，领头人就是那个大洋国总统。
		
		- 我听说过有关真理会的事
			Player: 我有听说过有关真理会的事。
			...是吗？
			原来你也早就知道了啊。
		
		但那个男人...他在真理会里甚至只是一个微不足道的边缘小人物，
		就连那个案子也是因为机缘巧合才被曝光出来的。
		（抽泣）
		啊，真主啊...在我们看不到的地方，又有多少个像她一样受折磨的人？
		
		- Player: 这...
		- Player: 之后呢？
		
		后来，我想起了小布给我的相机，她曾经用这个相机拍过许多见不得光的东西。
		她以前跟我说过自己是怎么一次又一次深入到危险的地方，调查这个社会的阴暗面的。
		虽然我以前很不喜欢她这样做，觉得那些照片最后都会被欧亚国用来抹黑我们国家。
		但...我也决定像她一样，独自展开一些调查。
		随着调查的深入，我开始接二连三地开始受到警察的骚扰、威胁，警告我不能这样。
		但我还是成功得到了有用的情报：
		有很多妇女儿童被暗地里送到大洋国西方海域的一个海岛上。
		我花了很多时间辗转在西海岸的港口一带，每天观察来往的船只。
		...但我毕竟不是小布那样专业的记者。
		每次当我举起来相机，就会有FPI的人就蜂拥而至地来追我。
		有一次我没有跑掉，然后他们就一拥而上，把我按倒在了地上。
		他们抬着我，把我抬上那辆FPI的警车。
		我被关到了他们的监禁处。
		
		- Player: 后来发生了什么？
		
		他们让我脱光衣服。手抱在头上原地跳，检查我身上有没有藏东西。
		体检完了之后，他们就把我送进FPI的监禁处。
		他们在后面不停地吆喝，说快一点，快一点。
		像是在对待一个牲口一样把我赶进去。
		他们称呼我永远是用那一串编号，而不是用我的名字。
		他们对我一言一行都像是在对待一个真正的罪犯一样。
		不断地在提醒我说，我是这个国家的垃圾，是被欧亚国洗脑的异端分子。
		
		一天过去了，两天过去了...
		
		后来他们问我，是不是欧亚国指使我的？
		知不知道是谁策划了这一切？
		欧亚国的人有没有给过我钱，他们的目的是什么？
		好像他们对于任何人都永远都是这几个问题。
		永远在假设说，我们背后有一个神秘组织，有一个邪恶的欧亚国在操控我们。
		哪怕我一遍一遍跟他们说：我爱着大洋国，我只是想要让这个国家变得更好。
		但是他们就是不愿意去相信这点。
		
		Player: ......
		
		三天过去了，他们又找到我。
		他们想让我再拿出更诚恳一点的态度，所以说要给我拍一段视频。
		他们要我在视频里面讲，我这样是不对的，要我对我自己的行为后悔。
		他们要我说我是一个罪人，一个异端分子。
		说我对不起大洋国，对不起英社和总统，也对不起自己身边的人。
		他们跟我说，我只要录了这段视频，他们保证我一定可以出去。
		我知道，这个就是老大哥时代的电视认罪。
		如果我这么做了，那我就真的成了洗不清的罪人了。
		......
		整整七天过去了。
		我撑不住了。我不知道他们会再拿出来什么样的手段。
		
		- Player: 天哪...
		- Player: 我，我从来不知道FPI竟然会做出这样的暴行...
		
#		我也许该庆幸自己是被FPI的人抓住，而不是真理会的人。
#		[shake rate=20 level=10]我,我都不敢想象被他们抓住之后会受到怎样的折磨...[/shake]
		
		在那之后，我没敢联系任何人...我不想让我身边的人也陷入危险...
		在经历了这些以后，我再也没法回到正常的生活了。
		他们给我的脚踝上戴上了电子镣铐，让我不能到任何离家两英里之外的地方。
		除了无处不在的AI助手之外，我每天无时无刻不被FPI的人监视，没有一丝隐私可言。
		我再也受不了了。
		也许我当初就不该去干什么蠢透了的调查...
		那些真理会的受害者...他们到底跟我有什么关系啊？
		我都自身难保了，为什么还要在乎他们的死活啊？
		我唯一知道的就是，我需要自由。
		我要离开这个国家，越远越好。
		
		...终于，大流行缠上了我们这座城市，我的机会来了。
		不久英社就发布了禁足令，而我当时正好在城市档案馆里。
		在破解建筑师的密文之后，我趁着夜晚钻进了下水道，通过地图上的方向出了城。
		我到了[color=crimson]小布在城外南边教区的安全网[/color]，但他不在那儿...
		我打了好多电话，但是再也没法联系到他了。
		
		
		在那里等了好几天之后，我意识到不能再这样了。
		于是我准备好几天的补给，骑着你的摩托车离开了
		之后的事情，你就都知道了。
		
		
		
		
		